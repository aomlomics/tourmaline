import pandas as pd
from tabulate import tabulate

# GLOBALS ---------------------------------------------------------------------

configfile: "config.yaml"

# PSEUDO-RULES ----------------------------------------------------------------

rule dada2_pe_denoise:
    input:
        "01-imported/fastq_pe.qzv",
        "01-imported/fastq_pe_count_describe.md",
        "01-imported/fastq_illumina_run.log",
        "02-output-dada2-pe/00-table-repseqs/table.qzv",
        "02-output-dada2-pe/00-table-repseqs/table_summary_samples.txt",
        "02-output-dada2-pe/00-table-repseqs/table_summary_features.txt",
        "02-output-dada2-pe/00-table-repseqs/representative_sequences.qzv",
        "02-output-dada2-pe/00-table-repseqs/representative_sequences_amplicon_type.txt",
        "02-output-dada2-pe/00-table-repseqs/representative_sequences_lengths_describe.md"

rule dada2_pe_diversity:
    input:
        "02-output-dada2-pe/01-alignment-tree-taxonomy/taxonomy.qzv",
        "02-output-dada2-pe/01-alignment-tree-taxonomy/rooted_tree.qza",
        # "02-output-dada2-pe/01-alignment-tree-taxonomy/rooted_tree.qzv",
        "02-output-dada2-pe/01-alignment-tree-taxonomy/aligned_dna_sequences_gaps_describe.md",
        "02-output-dada2-pe/02-tax-diversity/taxa_barplot.qzv",
        "02-output-dada2-pe/03-alpha-diversity/alpha_rarefaction.qzv",
        "02-output-dada2-pe/03-alpha-diversity/faith_pd_group_significance.qzv",
        "02-output-dada2-pe/03-alpha-diversity/observed_features_group_significance.qzv",
        "02-output-dada2-pe/03-alpha-diversity/shannon_group_significance.qzv",
        "02-output-dada2-pe/03-alpha-diversity/evenness_group_significance.qzv",
        "02-output-dada2-pe/04-beta-diversity/unweighted_unifrac_emperor.qzv",
        "02-output-dada2-pe/04-beta-diversity/unweighted_unifrac_group_significance.qzv"

rule dada2_pe_report:
    input:
        "03-reports/report_dada2-pe.html"

# -----------------------------------------------------------------------------

rule dada2_se_denoise:
    input:
        "01-imported/fastq_pe.qzv",
        "01-imported/fastq_pe_count_describe.md",
        "01-imported/fastq_illumina_run.log",
        "02-output-dada2-se/00-table-repseqs/table.qzv",
        "02-output-dada2-se/00-table-repseqs/table_summary_samples.txt",
        "02-output-dada2-se/00-table-repseqs/table_summary_features.txt",
        "02-output-dada2-se/00-table-repseqs/representative_sequences.qzv",
        "02-output-dada2-se/00-table-repseqs/representative_sequences_amplicon_type.txt",
        "02-output-dada2-se/00-table-repseqs/representative_sequences_lengths_describe.md"

rule dada2_se_diversity:
    input:
        "02-output-dada2-se/01-alignment-tree-taxonomy/taxonomy.qzv",
        "02-output-dada2-se/01-alignment-tree-taxonomy/rooted_tree.qza",
        # "02-output-dada2-se/01-alignment-tree-taxonomy/rooted_tree.qzv"
        "02-output-dada2-se/01-alignment-tree-taxonomy/aligned_dna_sequences_gaps_describe.md",
        "02-output-dada2-se/02-tax-diversity/taxa_barplot.qzv",
        "02-output-dada2-se/03-alpha-diversity/alpha_rarefaction.qzv",
        "02-output-dada2-se/03-alpha-diversity/faith_pd_group_significance.qzv",
        "02-output-dada2-se/03-alpha-diversity/observed_features_group_significance.qzv",
        "02-output-dada2-se/03-alpha-diversity/shannon_group_significance.qzv",
        "02-output-dada2-se/03-alpha-diversity/evenness_group_significance.qzv",
        "02-output-dada2-se/04-beta-diversity/unweighted_unifrac_emperor.qzv",
        "02-output-dada2-se/04-beta-diversity/unweighted_unifrac_group_significance.qzv"

rule dada2_se_report:
    input:
        "03-reports/report_dada2-se.html"

# -----------------------------------------------------------------------------

rule deblur_se_denoise:
    input:
        "01-imported/fastq_pe.qzv",
        "01-imported/fastq_pe_count_describe.md",
        "01-imported/fastq_illumina_run.log",
        "02-output-deblur-se/00-table-repseqs/table.qzv",
        "02-output-deblur-se/00-table-repseqs/table_summary_samples.txt",
        "02-output-deblur-se/00-table-repseqs/table_summary_features.txt",
        "02-output-deblur-se/00-table-repseqs/representative_sequences.qzv",
        "02-output-deblur-se/00-table-repseqs/representative_sequences_amplicon_type.txt",
        "02-output-deblur-se/00-table-repseqs/representative_sequences_lengths_describe.md"

rule deblur_se_diversity:
    input:
        "02-output-deblur-se/01-alignment-tree-taxonomy/taxonomy.qzv",
        "02-output-deblur-se/01-alignment-tree-taxonomy/rooted_tree.qza",
        # "02-output-deblur-se/01-alignment-tree-taxonomy/rooted_tree.qzv",
        "02-output-deblur-se/01-alignment-tree-taxonomy/aligned_dna_sequences_gaps_describe.md",
        "02-output-deblur-se/02-tax-diversity/taxa_barplot.qzv",
        "02-output-deblur-se/03-alpha-diversity/alpha_rarefaction.qzv",
        "02-output-deblur-se/03-alpha-diversity/faith_pd_group_significance.qzv",
        "02-output-deblur-se/03-alpha-diversity/observed_features_group_significance.qzv",
        "02-output-deblur-se/03-alpha-diversity/shannon_group_significance.qzv",
        "02-output-deblur-se/03-alpha-diversity/evenness_group_significance.qzv",
        "02-output-deblur-se/04-beta-diversity/unweighted_unifrac_emperor.qzv",
        "02-output-deblur-se/04-beta-diversity/unweighted_unifrac_group_significance.qzv"

rule deblur_se_report:
    input:
        "03-reports/report_deblur-se.html"

# RULES -----------------------------------------------------------------------

rule import_fastq_se_demux:
    input:
        config["manifest_se"]
    output:
        "01-imported/fastq_se.qza"
    shell:
        "qiime tools import "
        "--type 'SampleData[SequencesWithQuality]' "
        "--input-path {input} "
        "--output-path {output} "
        "--input-format SingleEndFastqManifestPhred33"

rule import_fastq_pe_demux:
    input:
        config["manifest_pe"]
    output:
        "01-imported/fastq_pe.qza"
    shell:
        "qiime tools import "
        "--type 'SampleData[PairedEndSequencesWithQuality]' "
        "--input-path {input} "
        "--output-path {output} "
        "--input-format PairedEndFastqManifestPhred33"

rule summarize_fastq_pe_demux:
    input:
        "01-imported/fastq_pe.qza"
    output:
        "01-imported/fastq_pe.qzv"
    shell:
        "qiime demux summarize "
        "--i-data {input} "
        "--o-visualization {output}"

# mac: gzcat (zcat requires extension .Z) / linux: zcat
# change gzcat/zcat to cat if fastq files are not gzipped (but they should be)
rule count_fastq_pe_demux:
    input:
        config["manifest_pe"]
    output:
        "01-imported/fastq_pe_count.csv"
    shell:
        "for line in `tail -n +2 {input} | cut -d',' -f2`; "
        "do echo -n $line; "
        "echo -n ','; "
        "gzcat $line | awk 'END {{l=NR; print l/4}}'; "
        "done > {output}"

rule fastq_pe_count_describe:
    input:
        "01-imported/fastq_pe_count.csv"
    output:
        "01-imported/fastq_pe_count_describe.md"
    run:
        s = pd.read_csv(input[0], header=None)
        t = s.describe()
        outstr = tabulate(t.iloc[1:], tablefmt="pipe", headers=['Statistic (n=%s)' % t.iloc[0].values[0].astype(int), 'Fastq sequences per sample'])
        with open(output[0], 'w') as target:
            target.write(outstr)
            target.write('\n')

# mac: gzcat (zcat requires extension .Z) / linux: zcat
# change gzcat/zcat to cat if fastq files are not gzipped (but they should be)
rule check_illumina_run:
    input:
        config["manifest_pe"]
    output:
        runs="01-imported/fastq_illumina_run.txt",
        log="01-imported/fastq_illumina_run.log"
    shell:
        "set +o pipefail; "
        "for line in `tail -n +2 {input} | cut -d',' -f2`; "
        "do echo -n $line; "
        "echo -n ','; "
        "gzcat $line | head -n 1 | sed 's/^@//' | cut -d ':' -f1-3; "
        "done > {output.runs}; "
        "echo -n 'Number of Illumina runs in {input}:' > {output.log}; cat {output.runs} | cut -d',' -f2 | sort | uniq | wc -l >> {output.log}"

rule import_ref_seqs:
    input:
        config["refseqs"]
    output:
        "01-imported/refseqs.qza"
    shell:
        "qiime tools import "
        "--type 'FeatureData[Sequence]' "
        "--input-path {input} "
        "--output-path {output}"

rule import_ref_tax:
    input:
        config["reftax"]
    output:
        "01-imported/reftax.qza"
    shell:
        "qiime tools import "
        "--type 'FeatureData[Taxonomy]' "
        '--input-format HeaderlessTSVTaxonomyFormat '
        "--input-path {input} "
        "--output-path {output}"

rule denoise_dada2_pe:
    input:
        "01-imported/fastq_pe.qza"
    params:
        trunclenf=config["dada2pe_trunc_len_f"],
        trunclenr=config["dada2pe_trunc_len_r"],
        trimleftf=config["dada2pe_trim_left_f"],
        trimleftr=config["dada2pe_trim_left_r"],
        maxeef=config["dada2pe_max_ee_f"],
        maxeer=config["dada2pe_max_ee_r"],
        truncq=config["dada2pe_trunc_q"],
        chimeramethod=config["dada2pe_chimera_method"],
        minfoldparentoverabundance=config["dada2pe_min_fold_parent_over_abundance"],
        nthreads=config["dada2pe_n_threads"],
        nreadslearn=config["dada2pe_n_reads_learn"],
        hashedfeatureids=config["dada2pe_hashed_feature_ids"]
    output:
        table="02-output-dada2-pe/00-table-repseqs/table.qza",
        repseqs="02-output-dada2-pe/00-table-repseqs/representative_sequences.qza",
        stats="02-output-dada2-pe/00-table-repseqs/stats.qza"
    shell:
        "qiime dada2 denoise-paired "
        "--i-demultiplexed-seqs {input} "
        "--p-trunc-len-f {params.trunclenf} "
        "--p-trunc-len-r {params.trunclenr} "
        "--p-trim-left-f {params.trimleftf} "
        "--p-trim-left-r {params.trimleftr} "
        "--p-max-ee-f {params.maxeef} "
        "--p-max-ee-r {params.maxeer} "
        "--p-trunc-q {params.truncq} "
        "--p-chimera-method {params.chimeramethod} "
        "--p-min-fold-parent-over-abundance {params.minfoldparentoverabundance} "
        "--p-n-threads {params.nthreads} "
        "--p-n-reads-learn {params.nreadslearn} "
        "{params.hashedfeatureids} "
        "--o-table {output.table} "
        "--o-representative-sequences {output.repseqs} "
        "--o-denoising-stats {output.stats} "
        "--verbose"

rule denoise_dada2_se:
    input:
        "01-imported/fastq_se.qza"
    params:
        trunclen=config["dada2se_trunc_len"],
        trimleft=config["dada2se_trim_left"],
        maxee=config["dada2se_max_ee"],
        truncq=config["dada2se_trunc_q"],
        chimeramethod=config["dada2se_chimera_method"],
        minfoldparentoverabundance=config["dada2se_min_fold_parent_over_abundance"],
        nthreads=config["dada2se_n_threads"],
        nreadslearn=config["dada2se_n_reads_learn"],
        hashedfeatureids=config["dada2se_hashed_feature_ids"]
    output:
        table="02-output-dada2-se/00-table-repseqs/table.qza",
        repseqs="02-output-dada2-se/00-table-repseqs/representative_sequences.qza",
        stats="02-output-dada2-se/00-table-repseqs/stats.qza"
    shell:
        "qiime dada2 denoise-single "
        "--i-demultiplexed-seqs {input} "
        "--p-trunc-len {params.trunclen} "
        "--p-trim-left {params.trimleft} "
        "--p-max-ee {params.maxee} "
        "--p-trunc-q {params.truncq} "
        "--p-chimera-method {params.chimeramethod} "
        "--p-min-fold-parent-over-abundance {params.minfoldparentoverabundance} "
        "--p-n-threads {params.nthreads} "
        "--p-n-reads-learn {params.nreadslearn} "
        "{params.hashedfeatureids} "
        "--o-table {output.table} "
        "--o-representative-sequences {output.repseqs} "
        "--o-denoising-stats {output.stats} "
        "--verbose"

rule denoise_deblur_se:
    input:
        seqs="01-imported/fastq_se.qza",
        refseqs="01-imported/refseqs.qza"
    params:
        trimlength=config["deblur_trim_length"],
        samplestats=config["deblur_sample_stats"],
        meanerror=config["deblur_mean_error"],
        indelprob=config["deblur_indel_prob"],
        indelmax=config["deblur_indel_max"],
        minreads=config["deblur_min_reads"],
        minsize=config["deblur_min_size"],
        jobstostart=config["deblur_jobs_to_start"],
        hashedfeatureids=config["deblur_hashed_feature_ids"]
    output:
        table="02-output-deblur-se/00-table-repseqs/table.qza",
        repseqs="02-output-deblur-se/00-table-repseqs/representative_sequences.qza",
        stats="02-output-deblur-se/00-table-repseqs/stats.qza"
    shell:
        "qiime deblur denoise-other "
        "--i-demultiplexed-seqs {input.seqs} "
        "--i-reference-seqs {input.refseqs} "
        "--p-trim-length {params.trimlength} "
        "{params.samplestats} "
        "--p-mean-error {params.meanerror} "
        "--p-indel-prob {params.indelprob} "
        "--p-indel-max {params.indelmax} "
        "--p-min-reads {params.minreads} "
        "--p-min-size {params.minsize} "
        "--p-jobs-to-start {params.jobstostart} "
        "{params.hashedfeatureids} "        
        "--o-table {output.table} "
        "--o-representative-sequences {output.repseqs} "
        "--o-stats {output.stats} "
        "--verbose"

rule summarize_feature_table:
    input:
        table="02-output-{method}/00-table-repseqs/table.qza",
        metadata=config["metadata"]
    output:
        "02-output-{method}/00-table-repseqs/table.qzv"
    shell:
        "qiime feature-table summarize "
        "--i-table {input.table} "
        "--m-sample-metadata-file {input.metadata} "
        "--o-visualization {output}"

rule unzip_table_to_biom:
    input:
        "02-output-{method}/00-table-repseqs/table.qza"
    output:
        "02-output-{method}/00-table-repseqs/table.biom"
    shell:
        "unzip -o {input} -d temp; "
        "mv temp/*/data/feature-table.biom {output}; "
        "/bin/rm -r temp"

rule summarize_biom_samples:
    input:
        "02-output-{method}/00-table-repseqs/table.biom"
    output:
        "02-output-{method}/00-table-repseqs/table_summary_samples.txt"
    shell:
        "biom summarize-table "
        "--input-fp {input} "
        "--output-fp {output}; "
        "cat {output} | sed 's/observation/feature/g' | sed 's/.000$//' > temp; "
        "mv temp {output}"

rule summarize_biom_features:
    input:
        "02-output-{method}/00-table-repseqs/table.biom"
    output:
        "02-output-{method}/00-table-repseqs/table_summary_features.txt"
    shell:
        "biom summarize-table "
        "--observations "
        "--input-fp {input} "
        "--output-fp {output}; "
        "cat {output} | sed 's/observation/feature/g' | sed 's/Counts\/sample/Counts\/feature/g' | sed 's/.000$//' > temp; "
        "mv temp {output}"

rule visualize_repseqs:
    input:
        "02-output-{method}/00-table-repseqs/representative_sequences.qza"
    output:
        "02-output-{method}/00-table-repseqs/representative_sequences.qzv"
    shell:
        "qiime feature-table tabulate-seqs "
        "--i-data {input} "
        "--o-visualization {output}"

rule unzip_repseq_to_fasta:
    input:
        "02-output-{method}/00-table-repseqs/representative_sequences.qza"
    output:
        "02-output-{method}/00-table-repseqs/representative_sequences.fasta"
    shell:
        "unzip -o {input} -d temp; "
        "mv temp/*/data/dna-sequences.fasta {output}; "
        "/bin/rm -r temp"

rule repseq_detect_amplicon_locus:
    input:
        "02-output-{method}/00-table-repseqs/representative_sequences.fasta"
    output:
        "02-output-{method}/00-table-repseqs/representative_sequences_amplicon_type.txt"
    shell:
        "python scripts/detect_amplicon_locus.py -i {input} > {output}"

rule repseq_length_distribution:
    input:
        "02-output-{method}/00-table-repseqs/representative_sequences.fasta"
    output:
        "02-output-{method}/00-table-repseqs/representative_sequences_lengths.txt"
    shell:
        "perl scripts/fastaLengths.pl {input} > {output}"

rule repseq_length_distribution_describe:
    input:
        "02-output-{method}/00-table-repseqs/representative_sequences_lengths.txt"
    output:
        "02-output-{method}/00-table-repseqs/representative_sequences_lengths_describe.md"
    run:
        s = pd.read_csv(input[0], header=None, index_col=0)
        t = s.describe()
        outstr = tabulate(t.iloc[1:], tablefmt="pipe", headers=['Statistic (n=%s)' % t.iloc[0].values[0].astype(int), 'Sequence length'])
        with open(output[0], 'w') as target:
            target.write(outstr)
            target.write('\n')

rule feature_classifier:
    input:
        refseqs="01-imported/refseqs.qza",
        reftax="01-imported/reftax.qza",
        repseqs="02-output-{method}/00-table-repseqs/representative_sequences.qza"
    output:
        "02-output-{method}/01-alignment-tree-taxonomy/taxonomy.qza"
    params:
        classifymethod=config["classify_method"]
    shell:
        "echo classify_method: {params.classifymethod}; "
        "if [ {params.classifymethod} = 'naive-bayes' ]; then "
        "if [ ! -f 01-imported/classifier.qza ]; then "
        "qiime feature-classifier fit-classifier-naive-bayes "
        "--i-reference-reads {input.refseqs} "
        "--i-reference-taxonomy {input.reftax} "
        "--o-classifier 01-imported/classifier.qza; "
        "fi; "
        "qiime feature-classifier classify-sklearn "
        "--i-classifier 01-imported/classifier.qza "
        "--i-reads {input.repseqs} "
        "--o-classification {output}; "
        "elif [ {params.classifymethod} = 'consensus-blast' ]; then "
        "qiime feature-classifier classify-consensus-blast "
        "--i-reference-reads {input.refseqs} "
        "--i-reference-taxonomy {input.reftax} "
        "--i-query {input.repseqs} "
        "--o-classification {output}; "
        "fi"

rule visualize_taxonomy:
    input:
        "02-output-{method}/01-alignment-tree-taxonomy/taxonomy.qza"
    output:
        "02-output-{method}/01-alignment-tree-taxonomy/taxonomy.qzv"
    shell:
        "qiime metadata tabulate "
        "--m-input-file {input} "
        "--o-visualization {output}"

rule taxa_barplot:
    input:
        table="02-output-{method}/00-table-repseqs/table.qza",
        taxonomy="02-output-{method}/01-alignment-tree-taxonomy/taxonomy.qza",
        metadata=config["metadata"]
    output:
        "02-output-{method}/02-tax-diversity/taxa_barplot.qzv"
    shell:
        "qiime taxa barplot "
        "--i-table {input.table} "
        "--i-taxonomy {input.taxonomy} "
        "--m-metadata-file {input.metadata} "
        "--o-visualization {output}"

rule alignment_mafft:
    input:
        "02-output-{method}/00-table-repseqs/representative_sequences.qza"
    output:
        "02-output-{method}/01-alignment-tree-taxonomy/aligned_representative_sequences.qza"
    shell:
        "qiime alignment mafft "
        "--i-sequences {input} "
        "--o-alignment {output}"

rule alignment_mask:
    input:
        "02-output-{method}/01-alignment-tree-taxonomy/aligned_representative_sequences.qza"
    output:
        "02-output-{method}/01-alignment-tree-taxonomy/masked_aligned_representative_sequences.qza"
    shell:
        "qiime alignment mask "
        "--i-alignment {input} "
        "--o-masked-alignment {output}"

rule phylogeny_fasttree:
    input:
        "02-output-{method}/01-alignment-tree-taxonomy/masked_aligned_representative_sequences.qza"
    output:
        "02-output-{method}/01-alignment-tree-taxonomy/unrooted_tree.qza"
    shell:
        "qiime phylogeny fasttree "
        "--i-alignment {input} "
        "--o-tree {output}"

rule phylogeny_midpoint_root:
    input:
        "02-output-{method}/01-alignment-tree-taxonomy/unrooted_tree.qza"
    output:
        "02-output-{method}/01-alignment-tree-taxonomy/rooted_tree.qza"
    shell:
        "qiime phylogeny midpoint-root "
        "--i-tree {input} "
        "--o-rooted-tree {output}"

# skipping tree visualization until empress is part of core q2 release
# rule visualize_tree:
#     input:
#         tree="02-output-{method}/01-alignment-tree-taxonomy/rooted_tree.qza",
#         table="02-output-{method}/00-table-repseqs/table.qza",
#         metadata=config["metadata"],
#         taxonomy="02-output-{method}/01-alignment-tree-taxonomy/taxonomy.qza"
#     output:
#         "02-output-{method}/01-alignment-tree-taxonomy/rooted_tree.qzv"
#     shell:
#         "qiime empress plot "
#         "--i-tree {input.tree} "
#         "--i-feature-table {input.table} "
#         "--m-sample-metadata-file {input.metadata} "
#         "--m-feature-metadata-file {input.taxonomy} "
#         "--o-visualization {output}"

rule unzip_alignment_to_fasta:
    input:
        "02-output-{method}/01-alignment-tree-taxonomy/masked_aligned_representative_sequences.qza"
    output:
        "02-output-{method}/01-alignment-tree-taxonomy/aligned_dna_sequences.fasta"
    shell:
        "unzip -o {input} -d temp; "
        "mv temp/*/data/aligned-dna-sequences.fasta {output}; "
        "/bin/rm -r temp"

rule alignment_count_gaps:
    input:
        "02-output-{method}/01-alignment-tree-taxonomy/aligned_dna_sequences.fasta"
    output:
        "02-output-{method}/01-alignment-tree-taxonomy/aligned_dna_sequences_gaps.txt"
    shell:
        "cat {input} | grep -v '>' | sed 's/[^-]//g' | awk '{{ print length }}' > {output}"

rule alignment_gaps_distribution_describe:
    input:
        "02-output-{method}/01-alignment-tree-taxonomy/aligned_dna_sequences_gaps.txt"
    output:
        "02-output-{method}/01-alignment-tree-taxonomy/aligned_dna_sequences_gaps_describe.md"
    run:
        s = pd.read_csv(input[0], header=None)
        t = s.describe()
        outstr = tabulate(t.iloc[1:], tablefmt="pipe", headers=['Statistic (n=%s)' % t.iloc[0].values[0].astype(int), 'Alignment gaps per sequence'])
        with open(output[0], 'w') as target:
            target.write(outstr)
            target.write('\n')

rule diversity_alpha_rarefaction:
    input:
        table="02-output-{method}/00-table-repseqs/table.qza",
        phylogeny="02-output-{method}/01-alignment-tree-taxonomy/rooted_tree.qza",
        metadata=config["metadata"]
    params:
        maxdepth=config["alpha_max_depth"]
    output:
        "02-output-{method}/03-alpha-diversity/alpha_rarefaction.qzv"
    shell:
        "qiime diversity alpha-rarefaction "
        "--i-table {input.table} "
        "--i-phylogeny {input.phylogeny} "
        "--p-max-depth {params.maxdepth} "
        "--p-metrics faith_pd "
        "--p-metrics shannon "
        "--p-metrics observed_features "
        "--p-metrics chao1 "
        "--m-metadata-file {input.metadata} "
        "--o-visualization {output}"

rule diversity_core_metrics_phylogenetic:
    input:
        table="02-output-{method}/00-table-repseqs/table.qza",
        phylogeny="02-output-{method}/01-alignment-tree-taxonomy/rooted_tree.qza",
        metadata=config["metadata"]
    params:
        samplingdepth=config["core_sampling_depth"]
    output:
        rarefiedtable="02-output-{method}/03-alpha-diversity/rarefied_table.qza",
        faithpdvector="02-output-{method}/03-alpha-diversity/faith_pd_vector.qza",        
        observedfeaturesvector="02-output-{method}/03-alpha-diversity/observed_features_vector.qza",
        shannonvector="02-output-{method}/03-alpha-diversity/shannon_vector.qza",
        evennessvector="02-output-{method}/03-alpha-diversity/evenness_vector.qza",
        unweightedunifracdistancematrix="02-output-{method}/04-beta-diversity/unweighted_unifrac_distance_matrix.qza",
        weightedunifracdistancematrix="02-output-{method}/04-beta-diversity/weighted_unifrac_distance_matrix.qza",
        jaccarddistancematrix="02-output-{method}/04-beta-diversity/jaccard_distance_matrix.qza",
        braycurtisdistancematrix="02-output-{method}/04-beta-diversity/bray_curtis_distance_matrix.qza",
        unweightedunifracpcoaresults="02-output-{method}/04-beta-diversity/unweighted_unifrac_pcoa_results.qza",
        weightedunifracpcoaresults="02-output-{method}/04-beta-diversity/weighted_unifrac_pcoa_results.qza",
        jaccardpcoaresults="02-output-{method}/04-beta-diversity/jaccard_pcoa_results.qza",
        braycurtispcoaresults="02-output-{method}/04-beta-diversity/bray_curtis_pcoa_results.qza",
        unweightedunifracemperor="02-output-{method}/04-beta-diversity/unweighted_unifrac_emperor.qzv",
        weightedunifracemperor="02-output-{method}/04-beta-diversity/weighted_unifrac_emperor.qzv",
        jaccardemperor="02-output-{method}/04-beta-diversity/jaccard_emperor.qzv",
        braycurtisemperor="02-output-{method}/04-beta-diversity/bray_curtis_emperor.qzv"
    shell:
        "qiime diversity core-metrics-phylogenetic "
        "--i-table {input.table} "
        "--i-phylogeny {input.phylogeny} "
        "--p-sampling-depth {params.samplingdepth} "
        "--m-metadata-file {input.metadata} "
        "--o-rarefied-table {output.rarefiedtable} "
        "--o-faith-pd-vector {output.faithpdvector} "
        "--o-observed-features-vector {output.observedfeaturesvector} "
        "--o-shannon-vector {output.shannonvector} "
        "--o-evenness-vector {output.evennessvector} "
        "--o-unweighted-unifrac-distance-matrix {output.unweightedunifracdistancematrix} "
        "--o-weighted-unifrac-distance-matrix {output.weightedunifracdistancematrix} "
        "--o-jaccard-distance-matrix {output.jaccarddistancematrix} "
        "--o-bray-curtis-distance-matrix {output.braycurtisdistancematrix} "
        "--o-unweighted-unifrac-pcoa-results {output.unweightedunifracpcoaresults} "
        "--o-weighted-unifrac-pcoa-results {output.weightedunifracpcoaresults} "
        "--o-jaccard-pcoa-results {output.jaccardpcoaresults} "
        "--o-bray-curtis-pcoa-results {output.braycurtispcoaresults} "
        "--o-unweighted-unifrac-emperor {output.unweightedunifracemperor} "
        "--o-weighted-unifrac-emperor {output.weightedunifracemperor} "
        "--o-jaccard-emperor {output.jaccardemperor} "
        "--o-bray-curtis-emperor {output.braycurtisemperor}"

rule diversity_alpha_group_significance:
    input:
        alphadiversity="02-output-{method}/03-alpha-diversity/{metric}_vector.qza",
        metadata=config["metadata"]
    output:
        "02-output-{method}/03-alpha-diversity/{metric}_group_significance.qzv"
    shell:
        "qiime diversity alpha-group-significance "
        "--i-alpha-diversity {input.alphadiversity} "
        "--m-metadata-file {input.metadata} "
        "--o-visualization {output}"

rule diversity_beta_group_significance:
    input:
        distancematrix="02-output-{method}/04-beta-diversity/unweighted_unifrac_distance_matrix.qza",
        metadata=config["metadata"]
    params:
        column=config["beta_group_column"]
    output:
        "02-output-{method}/04-beta-diversity/unweighted_unifrac_group_significance.qzv"
    shell:
        "qiime diversity beta-group-significance "
        "--i-distance-matrix {input.distancematrix} "
        "--m-metadata-file {input.metadata} "
        "--m-metadata-column {params.column} "
        "--o-visualization {output} "
        "--p-pairwise"

rule summarize_metadata:
    input:
        metadata=config["metadata"]
    output:
        "03-reports/metadata_summary.md"
    run:
        df = pd.read_csv(input.metadata, sep='\t')
        cols1 = ['project_name', 'experiment_design_description', 
                 'target_gene', 'target_subfragment', 'pcr_primers', 
                 'pcr_primer_names', 'seq_platform', 'seq_model',
                 'seq_chemistry', 'seq_meth',
                 'run_center', 'run_date']
        cols2 = sorted(list(set(df.columns) - set(cols1)))
        cols = cols1 + cols2
        df2 = pd.DataFrame(columns =[0,1], index=cols)
        for col in cols:
            if col in df.columns:
                vc = df[col].value_counts()
                if vc.index.shape == (0,):
                    df2.loc[col, 0] = '(no values in column)'
                    df2.loc[col, 1] = '--'
                else:
                    df2.loc[col, 0] = vc.index[0]
                    df2.loc[col, 1] = vc.values[0]
            else:
                df2.loc[col, 0] = '(column not provided)'
                df2.loc[col, 1] = '--'
        df2.columns = ['Most common value', 'Count']
        df2.index.name = 'Column name'
        outstr = tabulate(df2, tablefmt="pipe", headers="keys")
        with open(output[0], 'w') as target:
            target.write(outstr)
            target.write('\n')

rule generate_report_md:
    input:
        configfile="config.yaml",
        mdsummary="03-reports/metadata_summary.md",
        fastq="01-imported/fastq_pe_count_describe.md",
        run="01-imported/fastq_illumina_run.log",
        amplicontype="02-output-{method}/00-table-repseqs/representative_sequences_amplicon_type.txt",
        repseqs="02-output-{method}/00-table-repseqs/representative_sequences_lengths_describe.md",
        alignment="02-output-{method}/01-alignment-tree-taxonomy/aligned_dna_sequences_gaps_describe.md",
        samples="02-output-{method}/00-table-repseqs/table_summary_samples.txt",
        features="02-output-{method}/00-table-repseqs/table_summary_features.txt",
        visrepseqs="02-output-{method}/00-table-repseqs/representative_sequences.qzv",
        vistaxonomy="02-output-{method}/01-alignment-tree-taxonomy/taxonomy.qzv",
        vistable="02-output-{method}/00-table-repseqs/table.qzv",
        vistaxbar="02-output-{method}/02-tax-diversity/taxa_barplot.qzv",
        visalpharare="02-output-{method}/03-alpha-diversity/alpha_rarefaction.qzv",
        visevengs="02-output-{method}/03-alpha-diversity/evenness_group_significance.qzv",
        visfaithgs="02-output-{method}/03-alpha-diversity/faith_pd_group_significance.qzv",
        visobsfeaturesgs="02-output-{method}/03-alpha-diversity/observed_features_group_significance.qzv",
        visshannongs="02-output-{method}/03-alpha-diversity/shannon_group_significance.qzv",
        visbcemp="02-output-{method}/04-beta-diversity/bray_curtis_emperor.qzv",
        visjacemp="02-output-{method}/04-beta-diversity/jaccard_emperor.qzv",
        viswuemp="02-output-{method}/04-beta-diversity/weighted_unifrac_emperor.qzv",
        visuwuemp="02-output-{method}/04-beta-diversity/unweighted_unifrac_emperor.qzv",
        visuwugs="02-output-{method}/04-beta-diversity/unweighted_unifrac_group_significance.qzv"
    output:
        "03-reports/report_{method}.md"
    shell:
        "echo '# Tourmaline Report' > {output};"
        "echo '' >> {output};"
        "echo Filename: {output} >> {output};"
        "echo '' >> {output};"
        "echo 'For information on Tourmaline outputs, visit [https://github.com/aomlomics/tourmaline](https://github.com/aomlomics/tourmaline).' >> {output};"
        "echo '' >> {output};"
        "echo 'To view the QIIME 2 visualization (.qzv) files, click to download, then drag and drop in [https://view.qiime2.org](https://view.qiime2.org).' >> {output};"
        "echo '' >> {output};"
        "echo '---' >> {output};"
        "echo '' >> {output};"
        "echo '## Metadata Summary' >> {output};"
        "echo '' >> {output};"
        "echo Filename: {input.mdsummary} >> {output};"
        "echo '' >> {output};"
        "echo 'The first 12 columns are recommended by the Tourmaline developers but may not have been provided.' >> {output};"
        "echo '' >> {output};"
        "cat {input.mdsummary} >> {output};"
        "echo '' >> {output};"
        "echo '' >> {output};"
        "echo '---' >> {output};"
        "echo '' >> {output};"
        "echo '## Fastq Sequences Information' >> {output};"
        "echo '' >> {output};"
        "echo '### Fastq sequences per sample' >> {output};"
        "echo '' >> {output};"
        "echo Filename: {input.fastq} >> {output};"
        "echo '' >> {output};"
        "cat {input.fastq} >> {output};"
        "echo '' >> {output};"
        "echo '### Number of Illumina runs' >> {output};"
        "echo '' >> {output};"
        "echo Filename: {input.run} >> {output};"
        "echo '' >> {output};"
        "echo '```' >> {output};"
        "head -n 5 {input.run} >> {output};"
        "echo '```' >> {output};"
        "echo '' >> {output};"
        "echo '---' >> {output};"
        "echo '' >> {output};"
        "echo '## Representative Sequences Information' >> {output};"
        "echo '' >> {output};"
        "echo '### Amplicon type' >> {output};"
        "echo '' >> {output};"
        "echo Filename: {input.amplicontype} >> {output};"
        "echo '' >> {output};"
        "echo '```' >> {output};"
        "head -n 5 {input.amplicontype} >> {output};"
        "echo '```' >> {output};"
        "echo '' >> {output};"
        "echo '### Sequence length' >> {output};"
        "echo '' >> {output};"
        "echo Filename: {input.repseqs} >> {output};"
        "echo '' >> {output};"
        "cat {input.repseqs} >> {output};"
        "echo '' >> {output};"
        "echo '### Alignment gaps per sequence' >> {output};"
        "echo '' >> {output};"
        "echo Filename: {input.alignment} >> {output};"
        "echo '' >> {output};"
        "cat {input.alignment} >> {output};"
        "echo '' >> {output};"
        "echo '### Visualization of representative sequences' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.visrepseqs}]\(../{input.visrepseqs}\) >> {output};"
        "echo '' >> {output};"
        "echo '### Visualization of taxonomy' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.vistaxonomy}]\(../{input.vistaxonomy}\) >> {output};"
        "echo '' >> {output};"
        "echo '---' >> {output};"
        "echo '' >> {output};"
        "echo '## Observation Table Information' >> {output};"
        "echo '' >> {output};"
        "echo '### Table summary: Samples' >> {output};"
        "echo '' >> {output};"
        "echo Filename: {input.samples} >> {output};"
        "echo '' >> {output};"
        "echo '```' >> {output};"
        "head -n 13 {input.samples} >> {output};"
        "echo '```' >> {output};"
        "echo '' >> {output};"
        "echo '### Table summary: Features' >> {output};"
        "echo '' >> {output};"
        "echo Filename: {input.features} >> {output};"
        "echo '' >> {output};"
        "echo '```' >> {output};"
        "head -n 13 {input.features} >> {output};"
        "echo '```' >> {output};"
        "echo '' >> {output};"
        "echo '### Table summary: QIIME 2 visualization' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.vistable}]\(../{input.vistable}\) >> {output};"
        "echo '' >> {output};"
        "echo '---' >> {output};"
        "echo '' >> {output};"
        "echo '## Taxonomic Diversity Results' >> {output};"
        "echo '' >> {output};"
        "echo '### Taxonomy barplot' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.vistaxbar}]\(../{input.vistaxbar}\) >> {output};"
        "echo '' >> {output};"
        "echo '---' >> {output};"
        "echo '' >> {output};"
        "echo '## Alpha Diversity Results' >> {output};"
        "echo '' >> {output};"
        "echo '### Alpha rarefaction' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.visalpharare}]\(../{input.visalpharare}\) >> {output};"
        "echo '' >> {output};"
        "echo '### Evenness group significance' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.visevengs}]\(../{input.visevengs}\) >> {output};"
        "echo '' >> {output};"
        "echo '### Faith PD group significance' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.visfaithgs}]\(../{input.visfaithgs}\) >> {output};"
        "echo '' >> {output};"
        "echo '### Observed features group significance' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.visobsfeaturesgs}]\(../{input.visobsfeaturesgs}\) >> {output};"
        "echo '' >> {output};"
        "echo '### Shannon group significance' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.visshannongs}]\(../{input.visshannongs}\) >> {output};"
        "echo '' >> {output};"
        "echo '---' >> {output};"
        "echo '' >> {output};"
        "echo '## Beta Diversity Results' >> {output};"
        "echo '' >> {output};"
        "echo '### Bray-Curtis PCoA' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.visbcemp}]\(../{input.visbcemp}\) >> {output};"
        "echo '' >> {output};"
        "echo '### Jaccard PCoA' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.visjacemp}]\(../{input.visjacemp}\) >> {output};"
        "echo '' >> {output};"
        "echo '### Weighted UniFrac PCoA' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.viswuemp}]\(../{input.viswuemp}\) >> {output};"
        "echo '' >> {output};"
        "echo '### Unweighted UniFrac PCoA' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.visuwuemp}]\(../{input.visuwuemp}\) >> {output};"
        "echo '' >> {output};"
        "echo '### Unweighted UniFrac Group Significance' >> {output};"
        "echo '' >> {output};"
        "echo Download file: [{input.visuwugs}]\(../{input.visuwugs}\) >> {output};"
        "echo '' >> {output};"
        "echo '---' >> {output};"
        "echo '' >> {output};"
        "echo '## Tourmaline Config File' >> {output};"
        "echo '' >> {output};"
        "echo Filename: {input.configfile} >> {output};"
        "echo '' >> {output};"
        "echo '```' >> {output};"
        "cat {input.configfile} >> {output};"
        "echo '```' >> {output};"

rule generate_report_html:
    input:
        markdown="03-reports/report_{method}.md"
    params:
        theme=config["report_theme"]
    output:
        "03-reports/report_{method}.html"
    shell:
        "pandoc -i {input} -o {output};"
        "echo '<!DOCTYPE html>' > header.html;"
        "echo '<html>' >> header.html;"
        "echo '<head>' >> header.html;"
        "echo '<link rel=\"stylesheet\" type=\"text/css\" href=\"../css/{params.theme}.css\">' >> header.html;"
        "echo '</head>' >> header.html;"
        "echo '<body>' >> header.html;"
        "echo '' >> header.html;"
        "cat header.html {output} > temp.html;"
        "echo '' >> temp.html;"
        "echo '</body>' >> temp.html;"
        "echo '</html>' >> temp.html;"
        "mv temp.html {output};"
        "/bin/rm header.html"



